#!/usr/bin/env fish

set termwin "footclient zsh -c"

function sync_icon
    if systemctl --user is-active --quiet backupDrive.service
        set total (easyclone get-status -t 2>/dev/null)
        set current (easyclone get-status -f 2>/dev/null)
        set empty_paths (easyclone get-status -e 2>/dev/null)

        if test $total -eq 0
            set percentage 0
        else
            set raw_percent (math "($current / $total) * 100")
            set  percentage (printf "%.1f" $raw_percent)
        end

        if test "$empty_paths" = "[]"
          set backup_icon "backup"
        else
          set backup_icon "backup-empty-paths"
          set tooltip "Empty paths:\r" (echo "$empty_paths" | sed "s/'/\"/g" | jq --raw-output '. | join("\r")')
        end

        printf "{\"text\": \"%s\", \"alt\": \"%s\", \"tooltip\": \"%s\", \"class\": \"\", \"percentage\": \"%s\"}" \
          "$percentage" "$backup_icon" "$tooltip" "$percentage"
    else
        echo ""
    end
end

function mic_icon
    set -l CARD_INDEX 1
    set -l CONTROL_NAME Capture
    set -l mic_status (amixer -c $CARD_INDEX sget $CONTROL_NAME | grep -o '\[[a-z]*\]')

    if string match -q "*[off]*" "$mic_status"
        echo "󰍭 muted"
    else
        echo ""
    end
end

function do_not_disturb
    if test (dunstctl is-paused) = "true"
        printf "󰂛\nPress Mod+Shift+B\n"
    else
        echo ""
    end
end

function check_updates
    set -l updates_arch 0
    set -l updates_aur 0

    while true
        if type -q checkupdates
            set updates_arch (checkupdates 2>/dev/null | wc -l)
            if test $status -eq 0
                break
            end
        else
            exit 1
        end
        dunstify -a "waybar" -i go-down "update error" "cannot fetch update count from checkupdates, retrying in 5s"
        sleep 5
    end

    if type -q paru
        set updates_aur (paru -Qum 2>/dev/null | wc -l)
        if test $status -ne 0
            set updates_aur 0
        end
    end

    set updates (math $updates_arch + $updates_aur)
    if test $updates -gt 0
        echo "󰏗 <span foreground=\"$matugen_on_surface\">$updates</span>"
    else
        echo ""
    end
end

function copy_date
    if date "+%F" | wl-copy
        dunstify -a "Waybar" -r 9326 -i clipit-trayicon -t 5000 "date copied to clipboard!" "'"(date "+%F")"'"
    else
        dunstify -a "Waybar" -r 9326 -i clipit-trayicon -t 5000 "something happened while copying date to clipboard." "check the script file $argv[0]."
    end
end

function gpu_usage
    echo "󰹑 <span foreground=\"$matugen_on_surface\">"(cat /sys/class/hwmon/hwmon3/device/gpu_busy_percent)"%</span>"
end

function wifi_click
    if ping -q -c 1 -W 1 8.8.8.8 >/dev/null 2>&1
        $termwin '
            read -p "are you sure you want to run nethogs? (y/n) " -n 1 -r
            echo
            if [[ $reply =~ ^[yy]$ ]]; then
                printf "\e[1menter your password to monitor the network activity:\e[0m\n" &&
                sudo nethogs
            fi
        '
    else if rfkill | grep -w "blocked" >/dev/null
        $termwin 'printf "\e[1mEnter your password to unblock network:\n" && sudo rfkill unblock all && impala'
    else
        $termwin 'impala'
    end
end

function bt_toggle
    if test (bluetoothctl show | grep "Powered: yes" | wc -c) -eq 0
        bluetoothctl power on
    else
        bluetoothctl power off
    end
end

function switch_audio_sink
    if not pactl list short sinks | grep -q "combined"
        pactl load-module module-combine-sink sink_name=combined sink_properties=device.description="CombinedOutput" slaves=alsa_output.pci-0000_04_00.6.analog-stereo,alsa_output.pci-0000_04_00.1.hdmi-stereo-extra1
    end

    set DEFAULT_SINK combined
    set BLUETOOTH_SINK bluez_output.00_A4_1C_1D_BE_79.1

    if not pactl list short sinks | grep -q "$BLUETOOTH_SINK"
        pactl set-default-sink "$DEFAULT_SINK"
    else
        if test (pactl info | grep "Default Sink" | awk -F: '{print $2}' | xargs) = "$BLUETOOTH_SINK"
            pactl set-default-sink "$DEFAULT_SINK"
        else
            pactl set-default-sink "$BLUETOOTH_SINK"
        end
    end
end

function check_special_workspace
    if hyprctl workspaces | grep "special:magic" >/dev/null
        printf "H\nPress Super+V\n"
    else
        echo ""
    end
end

function show_failed_units
    $termwin "echo 'Failed system services' && sudo systemctl --failed && echo '\nFailed user services' && systemctl --user --failed; zsh"
end

switch $argv[1]
    case sync_icon
        sync_icon
    case mic_icon
        mic_icon
    case do_not_disturb
        do_not_disturb
    case check_special_workspace
        check_special_workspace
    case switch_audio_sink
        switch_audio_sink
    case sync_icon_click
        eval "$termwin 'watch -c -n 0.5 tail -n 50 $HOME/.cache/logs/backupDrive.log'"
    case gpu_click
        eval "$termwin 'radeontop; zsh'"
    case copy_date
        copy_date
    case wifi_click
        wifi_click
    case gpu_usage
        gpu_usage
    case htop_cpu
        eval "$termwin 'htop -t --sort-key PERCENT_CPU'"
    case htop_mem
        eval "$termwin 'htop -t --sort-key PERCENT_MEM'"
    case check_updates
        check_updates
    case open_update_screen
        eval "$termwin 'printf \"%b\\n\\n\" \"\e[1mEnter your password to update Pacman/AUR packages:\e[0m\"; printf \"%b\\n\" \"\e[32m:: \e[0mImportant updates:\"; checkupdates | grep -Ff ~/.config/paru/important_packages.txt; sudo paru; zsh'"
    case bt_toggle
        bt_toggle
    case change_keyboard_state
        change_keyboard_state
    case show_failed_units
        show_failed_units
    case get_keyboard_state
        get_keyboard_state
    case '*'
        echo "Don't use this script from your terminal."
end
